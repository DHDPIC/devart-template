<!doctype html>
<html>
	<head>
		<meta charset="utf-8" />
		<title>devart sphere</title>
		<link type="text/css" rel="stylesheet" href="/css/display.css">
	</head>
	<body>
		<div id="container"></div>

		<p id="orientation">Orientation<br>
	        alpha: <span class="alpha"></span><br>
	        beta: <span class="beta"></span><br>
	        gamma: <span class="gamma"></span>
        </p>
	</body>

	<script type="x-shader/x-vertex" id="textureGenerationVertex">
		varying vec2 vUv;

		void main() {
			vUv = vec2(uv.s, uv.t);
			gl_Position = projectionMatrix * modelViewMatrix * vec4(position, 1.0);
		}
	</script>

	<script type="x-shader/x-fragment" id="textureGenerationFragment">
		uniform sampler2D uDeviceInputTexture;
		uniform sampler2D uSphereHeightMap1;
		uniform float uTime;
		uniform float uDelta;
		uniform vec2 uResolution;

		varying vec2 vUv;

		vec2 pixelCoords (vec2 texCoords, float width, float height) {
			vec2 p = vec2(0.0, 0.0);
			p.x = floor(texCoords.x / width);
			p.y = floor(texCoords.y / height);

			return p;
		}

		vec2 texCoords (vec2 pixelCoords, float width, float height, float halfPixelWidth, float halfPixelHeight) {
			vec2 t = vec2(0.0, 0.0);
			t.x = pixelCoords.x / width;
			t.y = pixelCoords.y / height;

			return t;
		}

		float map (float value, float sMin, float sMax, float dMin, float dMax) {
			return ((value / (sMax - sMin)) * (dMax - dMin)) + dMin;
		}

		void main () {
			vec4 color;
			vec4 data;
			vec2 uv = vUv;

			float width = uResolution.x;
			float height = uResolution.y;
			float pixelWidth = 1.0 / width;
			float halfPixelWidth = pixelWidth * 0.5;
			float pixelHeight = 1.0 / height;
			float halfPixelHeight = pixelHeight * 0.5;
			float alpha;
			float beta;
			float gamma;
			float active;
			float yp;
			float tolerance = pixelHeight;
			float diff;
			float speed = 1.0;
			float dy = pixelHeight;
			vec2 tc;

			if(vUv.t < (1.0 - dy)) {
				uv.t = vUv.t + dy;
				color = texture2D(uSphereHeightMap1, uv);
			}
			else {
				// only testing 1st coord right now
				//tc = texCoords (vec2(0.0, 0.0), width, height, halfPixelWidth, halfPixelHeight);
				data = texture2D(uDeviceInputTexture, vec2(0.0, 1.0));
				alpha = data.r;
				beta = data.g;
				gamma = data.b;
				active = data.a;
				yp = beta;

				diff = vUv.s - yp;

				if(active == 1.0 && abs(diff) < tolerance) {
					color = vec4(1.0, 0.0, 0.0, 0.0);
				}
				else {
					color = vec4(0.0, 0.0, 0.0, 1.0);
				}
			}

			gl_FragColor = color;
		}
	</script>

	<script type="x-shader/x-vertex" id="vertexshader">
		// switch on high precision floats
		#ifdef GL_ES
		precision highp float;
		#endif

		uniform sampler2D bumpTexture;
		uniform float bumpScale;
		uniform float uTime;

		varying float vAmount;
		varying vec2 vUV;

		varying vec3 vNormal;

		// 4 dimensional noise function
		// https://github.com/ashima/webgl-noise/blob/master/src/noise4D.glsl

		vec4 mod289(vec4 x) {
		  return x - floor(x * (1.0 / 289.0)) * 289.0;
		}

		float mod289(float x) {
		  return x - floor(x * (1.0 / 289.0)) * 289.0;
		}

		vec4 permute(vec4 x) {
		     return mod289(((x*34.0)+1.0)*x);
		}

		float permute(float x) {
		     return mod289(((x*34.0)+1.0)*x);
		}

		vec4 taylorInvSqrt(vec4 r) {
		  return 1.79284291400159 - 0.85373472095314 * r;
		}

		float taylorInvSqrt(float r) {
		  return 1.79284291400159 - 0.85373472095314 * r;
		}

		vec4 grad4(float j, vec4 ip) {
		  const vec4 ones = vec4(1.0, 1.0, 1.0, -1.0);
		  vec4 p,s;

		  p.xyz = floor( fract (vec3(j) * ip.xyz) * 7.0) * ip.z - 1.0;
		  p.w = 1.5 - dot(abs(p.xyz), ones.xyz);
		  s = vec4(lessThan(p, vec4(0.0)));
		  p.xyz = p.xyz + (s.xyz*2.0 - 1.0) * s.www;

		  return p;
		}

		// (sqrt(5) - 1)/4 = F4, used once below
		#define F4 0.309016994374947451

		float snoise(vec4 v) {
		  const vec4  C = vec4( 0.138196601125011,  // (5 - sqrt(5))/20  G4
		                        0.276393202250021,  // 2 * G4
		                        0.414589803375032,  // 3 * G4
		                       -0.447213595499958); // -1 + 4 * G4

		// First corner
		  vec4 i  = floor(v + dot(v, vec4(F4)) );
		  vec4 x0 = v -   i + dot(i, C.xxxx);

		// Other corners

		// Rank sorting originally contributed by Bill Licea-Kane, AMD (formerly ATI)
		  vec4 i0;
		  vec3 isX = step( x0.yzw, x0.xxx );
		  vec3 isYZ = step( x0.zww, x0.yyz );
		//  i0.x = dot( isX, vec3( 1.0 ) );
		  i0.x = isX.x + isX.y + isX.z;
		  i0.yzw = 1.0 - isX;
		//  i0.y += dot( isYZ.xy, vec2( 1.0 ) );
		  i0.y += isYZ.x + isYZ.y;
		  i0.zw += 1.0 - isYZ.xy;
		  i0.z += isYZ.z;
		  i0.w += 1.0 - isYZ.z;

		  // i0 now contains the unique values 0,1,2,3 in each channel
		  vec4 i3 = clamp( i0, 0.0, 1.0 );
		  vec4 i2 = clamp( i0-1.0, 0.0, 1.0 );
		  vec4 i1 = clamp( i0-2.0, 0.0, 1.0 );

		  //  x0 = x0 - 0.0 + 0.0 * C.xxxx
		  //  x1 = x0 - i1  + 1.0 * C.xxxx
		  //  x2 = x0 - i2  + 2.0 * C.xxxx
		  //  x3 = x0 - i3  + 3.0 * C.xxxx
		  //  x4 = x0 - 1.0 + 4.0 * C.xxxx
		  vec4 x1 = x0 - i1 + C.xxxx;
		  vec4 x2 = x0 - i2 + C.yyyy;
		  vec4 x3 = x0 - i3 + C.zzzz;
		  vec4 x4 = x0 + C.wwww;

		// Permutations
		  i = mod289(i);
		  float j0 = permute( permute( permute( permute(i.w) + i.z) + i.y) + i.x);
		  vec4 j1 = permute( permute( permute( permute (
		             i.w + vec4(i1.w, i2.w, i3.w, 1.0 ))
		           + i.z + vec4(i1.z, i2.z, i3.z, 1.0 ))
		           + i.y + vec4(i1.y, i2.y, i3.y, 1.0 ))
		           + i.x + vec4(i1.x, i2.x, i3.x, 1.0 ));

		// Gradients: 7x7x6 points over a cube, mapped onto a 4-cross polytope
		// 7*7*6 = 294, which is close to the ring size 17*17 = 289.
		  vec4 ip = vec4(1.0/294.0, 1.0/49.0, 1.0/7.0, 0.0) ;

		  vec4 p0 = grad4(j0,   ip);
		  vec4 p1 = grad4(j1.x, ip);
		  vec4 p2 = grad4(j1.y, ip);
		  vec4 p3 = grad4(j1.z, ip);
		  vec4 p4 = grad4(j1.w, ip);

		// Normalise gradients
		  vec4 norm = taylorInvSqrt(vec4(dot(p0,p0), dot(p1,p1), dot(p2, p2), dot(p3,p3)));
		  p0 *= norm.x;
		  p1 *= norm.y;
		  p2 *= norm.z;
		  p3 *= norm.w;
		  p4 *= taylorInvSqrt(dot(p4,p4));

		// Mix contributions from the five corners
		  vec3 m0 = max(0.6 - vec3(dot(x0,x0), dot(x1,x1), dot(x2,x2)), 0.0);
		  vec2 m1 = max(0.6 - vec2(dot(x3,x3), dot(x4,x4)            ), 0.0);
		  m0 = m0 * m0;
		  m1 = m1 * m1;
		  return 49.0 * ( dot(m0*m0, vec3( dot( p0, x0 ), dot( p1, x1 ), dot( p2, x2 )))
		               + dot(m1*m1, vec2( dot( p3, x3 ), dot( p4, x4 ) ) ) ) ;

		}

		void main() {
			vNormal = normal;
			vUV = uv;
			vec4 bumpData = texture2D(bumpTexture, uv);

			float noiseFactor = 10.0;
			float noiseStride = 0.025;
			// using 4d noise for best quality visual, although not as performant
			// could combine 3d noise in with the texture generation for increased speed but spherical distortion
			float noise = snoise(vec4(position.x * noiseStride, position.y * noiseStride, position.z * noiseStride, uTime));


			vAmount = bumpData.r; // get the r channel for height
			vec3 newPosition = position + (normal * bumpScale * vAmount) + (normal * noise * noiseFactor);
			gl_Position = projectionMatrix * modelViewMatrix * vec4(newPosition,1.0);
		}
	</script>

	<script type="x-shader/x-fragment" id="fragmentshader">
		#ifdef GL_ES
		precision highp float;
		#endif

		varying float vAmount;
		varying vec2 vUV;

		varying vec3 vNormal;

		void main()
		{
			vec3 light = vec3(0.5,0.8,0.0);
			light = normalize(light);
			float dProd = max(0.0, dot(vNormal,light));
			gl_FragColor 	= vec4(dProd/1.0,dProd/5.0,dProd/3.0,1.0);
			//gl_FragColor = vec4(1.0,1.0,1.0,1.0);
		}
	</script>

	<script src="/js/lib/jquery-1.11.0.min.js"></script>
	<script src="/js/lib/three.js"></script>
	<script src="/js/lib/socket.io.js"></script>

	<!-- this file has been intentionally left out of git, it contains ip and port of the node server running index.html
	var config = {
	  server:'<IPADDRESS>',
	  port:'<PORT>'
	};
	-->
	<script src="/js/src/serverconfig.js"></script>

	<script src="/js/src/FBOUtilities.js"></script>
	<script src="/js/src/display.js"></script>
</html>
